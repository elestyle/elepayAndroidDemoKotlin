plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'jp.elestyle.androidapp.elepaydemoapp'
    signingConfigs {
        config {
            keyAlias 'demokey'
            keyPassword 'password'
            storeFile file('../demo.jks')
            storePassword 'password'
        }
    }
    compileSdk 35
    defaultConfig {
        applicationId "jp.elestyle.androidapp.elepaydemoapp"
        minSdkVersion 23
        targetSdk 35
        versionCode 14
        versionName "1.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    // Exclude the debug config file.
    android.applicationVariants.configureEach { var ->
        if (var.buildType.name == 'release') {
            var.mergeAssets.doLast {
                delete(fileTree(dir: var.mergeAssets.outputDir, includes: ['config.json']))
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
//    implementation 'com.basgeekball:awesome-validation:4.1'

    // ElePay
    implementation 'jp.elestyle.androidapp:elepay:3.4.0'
}

repositories {
    maven {
        url "https://elestyle.github.io/elepay-android/repository"
    }
}
